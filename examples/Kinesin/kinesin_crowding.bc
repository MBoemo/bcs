//Kinsin molecular crowding from Boemo, Cardelli, and Nieduszynski (2020)

f = 10000; //fast rate
r = 1; //stepping rate
N = 20; //max number of kinesin in the system
L = 100; //microtubule length

//process definitions
K[i] = {i?[1..N](c),r*c}.K[i] 
     + {@step![i+1],r}.{@leave![i],f}.K[i+1];

T[] = {@step?[0..L](p),1}.({p?[0..N](c),f}.{p#[c],f}.{p![c+1],f}.T[]
                         + {~p?[0..N],f}.{p![1],f}.T[])
    + {@leave?[0..L](p),1}.({p?[1..N](c),f}.{p#[c],f}.{p![c-1],f}.T[]
                         + {~p?[1..N],f}.{p![0],f}.T[]);

//initial state of the system, 5 kinesin at each of positions 10,20,30,40
5*K[10] || 5*K[20] || 5*K[30] || 5*K[40] || T[];
